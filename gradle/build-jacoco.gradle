/*
	================================================================================
	This file contains the all configurations for scan jacoco in this project
	================================================================================

	Included from "${rootProject.projectDir}/build.gradle
	================================================================================
 */

def jacocoCoverage = 0.8
def jacocoExclude = [
        "/com/mercadolibre/coupon/application/**/*.*",
        "/com/mercadolibre/coupon/configuration/*.*",
        "/com/mercadolibre/coupon/crosscutting/**/*.*",
        "/com/mercadolibre/coupon/domain/**",
        "/com/mercadolibre/coupon/infrastructure/model/**",
        "/com/mercadolibre/coupon/infrastructure/outputpoint/**",
        "/com/mercadolibre/coupon/*CouponApplication*"
]

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    check.dependsOn jacocoTestCoverageVerification
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir("/reports/jacoco/test/jacocoTest.xml")
    }

    excludeClassFilesForReport(classDirectories, jacocoExclude)
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = jacocoCoverage
            }
        }
        excludeClassFilesForReport(classDirectories, jacocoExclude)
    }
}

private excludeClassFilesForReport(classDirectories, jacocoExclude) {
    classDirectories.setFrom(files(classDirectories.files.collect { fileTree(dir: it, exclude: jacocoExclude) }))
}